buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["FreeraspReactNative_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: 'kotlinx-serialization'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["FreeraspReactNative_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["FreeraspReactNative_" + name]).toInteger()
}

def getIntegerDefault(name) {
  return (project.properties["FreeraspReactNative_" + name]).toInteger()
}

android {
  compileSdkVersion getIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getIntegerDefault("minSdkVersion")
    targetSdkVersion getIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles "proguard-rules.pro"
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  if (project.android.hasProperty("namespace")) {
    namespace("com.freeraspreactnative")
  }
}

repositories {
  mavenCentral()
  google()
}

rootProject.allprojects {
  repositories {
    maven{url "https://europe-west3-maven.pkg.dev/talsec-artifact-repository/freerasp"}
    maven{url "https://jitpack.io"}
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")
def react_native_version = getExtOrDefault("reactNativeVersion")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:$react_native_version"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
  implementation "com.aheaditec.talsec.security:TalsecSecurity-Community-ReactNative:14.0.1"
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "FreeraspReactNative"
    codegenJavaPackageName = "com.freeraspreactnative"
  }
}
